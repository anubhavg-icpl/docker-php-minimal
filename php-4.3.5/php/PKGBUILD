pkgname=php
pkgver=4.3.5
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
source=("http://museum.php.net/php4/php-$pkgver.tar.gz")
md5sums=('da564d33f6fb2b8174a318b154c07c79')

build() {
    cd ${pkgname}-${pkgver}

    # Hack around an issue with ./configure not correctly detecting gcc due to
    # _FORTIFY_SOURCE
    export CPPFLAGS=

    ./configure --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc/php \
        --localstatedir=/var \
        --with-layout=GNU \
        --with-config-file-path=/etc/php \
        --with-config-file-scan-dir=/etc/php/conf.d \
        --disable-rpath \
        --mandir=/usr/share/man \
        --disable-cgi \
        --disable-all
    make
}

package() {
    pkgdesc='An HTML-embedded scripting language'
    depends=('pcre' 'bzip2')
    backup=('etc/php/php.ini')

    cd ${pkgname}-${pkgver}
    make -j1 INSTALL_ROOT=${pkgdir} install
    install -D -m644 ${pkgname}-${pkgver}/php.ini-recommended ${pkgdir}/etc/php/php.ini
    install -d -m755 ${pkgdir}/etc/php/conf.d/
}
