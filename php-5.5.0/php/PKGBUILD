pkgname=php
pkgver=5.5.0
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
source=("http://museum.php.net/php5/php-$pkgver.tar.gz")
md5sums=('a7d9598c0e60b47960b8e803e51c4309')

build() {
    local _phpconfig="--config-cache \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc/php \
        --localstatedir=/var \
        --with-layout=GNU \
        --with-config-file-path=/etc/php \
        --with-config-file-scan-dir=/etc/php/conf.d \
        --disable-rpath \
        --mandir=/usr/share/man \
        --disable-cgi \
        --disable-all"

    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure ${_phpconfig}
    make
}

package() {
    pkgdesc='An HTML-embedded scripting language'
    depends=('pcre' 'bzip2')
    backup=('etc/php/php.ini')

    cd ${srcdir}/${pkgname}-${pkgver}
    make -j1 INSTALL_ROOT=${pkgdir} install
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/php.ini-production ${pkgdir}/etc/php/php.ini
    install -d -m755 ${pkgdir}/etc/php/conf.d/

    # remove empty directory
    rmdir ${pkgdir}/usr/include/php/include
}
