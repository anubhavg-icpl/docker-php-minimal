# Contributor: Spencer Rinehart <anubis@overthemonkey.com>

pkgname=php
pkgver=5.0.1
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=()
source=("http://museum.php.net/php5/php-$pkgver.tar.gz" 'zend_modules.patch')
md5sums=('15c9f5a9848a9e8f32002c6731a31ca8' '913fc64bde894e49d2cdc35e7c2862ef')

build() {
    local _phpconfig="--prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc/php \
        --localstatedir=/var \
        --with-layout=GNU \
        --with-config-file-path=/etc/php \
        --with-config-file-scan-dir=/etc/php/conf.d \
        --disable-rpath \
        --mandir=/usr/share/man \
	--disable-cgi \
        --disable-all"

    cd ${srcdir}/${pkgname}-${pkgver}

    # Related to https://github.com/php/php-src/commit/de9206a9e9ff1ca95773d9812a986be778373467
    patch -p1 <${srcdir}/zend_modules.patch

    # Hack around an issue with ./configure not correctly detecting gcc due to
    # _FORTIFY_SOURCE
    export CPPFLAGS=

    ./configure ${_phpconfig}
    make
}

package() {
    pkgdesc='An HTML-embedded scripting language'
    depends=('pcre' 'bzip2')
    backup=('etc/php/php.ini')

    cd ${srcdir}/${pkgname}-${pkgver}
    make -j1 INSTALL_ROOT=${pkgdir} install
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/php.ini-recommended ${pkgdir}/etc/php/php.ini
    install -d -m755 ${pkgdir}/etc/php/conf.d/
}
